#!/bin/csh
# $Heade$
# $Log: mri2mpr_4dfp,v $
# Revision 1.1  1999/02/22  02:11:20  avi
# Initial revision
#
set idstr = '$Id: mri2mpr_4dfp,v 1.1 1999/02/22 02:11:20 avi Exp $'

echo $idstr
if (${#argv} < 2) then
	echo "Usage: mri2mpr_4dfp mri_anat mpr_anat [useold]"
	echo " e.g.: mri2mpr_4dfp anat_6run_ave 654-3"
	echo " N.B.: Any argument may include a path, e.g., /data/petmr1/data7/stem/96_06_14_stem9/scout/654-3"
	echo " N.B.: All named images must be in either ANALYZE or 4dfp format. ANALYZE will be converted to 4dfp"
	exit
endif

set date0 = `date`
set usr = `whoami`

@ useold = 0
@ debug = 0

set wrkdir =	$cwd
set bindir =	/data/petsun4/data1/solaris
set refdir =	/data/petsun9/data2/petmri
set oristr =	(T C S)

set xxx = ($1 $2)
set xxx = ($xxx:gr)
set xxx = ($xxx:gr)
set tmp = ($xxx:gh)
set mri = $xxx[1]:t
if (-d $tmp[1]:h) then
	set mridir = `pushd $tmp[1]:h | awk '{print $1}'`
else
	set mridir = $cwd
endif
set mpr = $xxx[2]:t
if (-d $tmp[2]:h) then
	set mprdir = `pushd $tmp[2]:h | awk '{print $1}'`
else
	set mprdir = $cwd
endif

@ k = 1
while ($k <= 2)
	     if (-e $xxx[$k]:t.4dfp.img && -e $xxx[$k]:t.4dfp.ifh) then
	else if (-e $xxx[$k]:t.img && -e $xxx[$k]:t.hdr) then
		$bindir/analyzeto4dfp $xxx[$k]:t
	else if (-e $xxx[$k]:t.4dint.img && -e $xxx[$k]:t.4dint.hdr) then
		$bindir/analyzeto4dfp $xxx[$k]:t.4dint
	else if (-e $xxx[$k].4dfp.img && -e $xxx[$k].4dfp.ifh) then
		foreach e (img img.rec ifh)
			ln -s $xxx[$k].4dfp.$e .
		end
	else if (-e $xxx[$k].img && -e $xxx[$k].hdr) then
		/bin/cp $xxx[$k].{img,img.rec,hdr} .
		$bindir/analyzeto4dfp $xxx[$k]:t
		/bin/rm $xxx[$k]:t.{img,img.rec,hdr}
	else if (-e $xxx[$k].4dint.img && -e $xxx[$k].4dint.hdr) then
		/bin/cp $xxx[$k].4dint.{img,img.rec,hdr} .
		$bindir/analyzeto4dfp $xxx[$k]:t.4dint
		/bin/rm $xxx[$k]:t.4dint.{img,img.rec,hdr}
	else	echo $xxx[$k] not found
		exit -1
	else
	endif
	@ k++
end

foreach x (${argv[2-]})
	if ($x == "debug")	@ debug++
	if ($x == "useold")	@ useold++	# use t4files on /tmp if they exist
end

set log = $mri"_mri2mpr_4dfp".log
touch				$log
date >>				$log
echo $0 $argv[1-] >>		$log
echo $idstr >>			$log
echo mri = $mridir/$mri >>	$log
echo mpr = $mprdir/$mpr >>	$log
tail -50 $log

set t4file = $mpr"_to_711-2A_t4"
if ($useold && ! -e $t4file && -e $mprdir/$t4file) cp $mprdir/$t4file .
if (! -e $t4file) $bindir/mpr2atl_4dfp $mpr

set t4file = $mri"_to_"$mpr"_t4"
if ($useold && -e $mridir/$t4file) /bin/cp $mridir/$t4file .
if (! $useold || ! -e $t4file) then
	if (! -e $mpr"_g15".4dfp.img || ! -e $mpr"_g15".4dfp.ifh) $bindir/gauss_4dfp $mpr 1.5
	if (! -e $mri"_g15".4dfp.img || ! -e $mri"_g15".4dfp.ifh) $bindir/gauss_4dfp $mri 1.5
	$bindir/msktgen_4dfp $mpr
	if ($status < 0) exit $status
	$bindir/mskgen_4dfp $mri
	if ($status < 0) exit $status

	@ ori = `awk '/orientation/{print $NF - 1}' $mpr.4dfp.ifh`
	$bindir/t4_inv $bindir/$oristr[$ori]_t4 temp$$_t4
	@ ori = `awk '/orientation/{print $NF - 1}' $mri.4dfp.ifh`
	$bindir/t4_mul $bindir/$oristr[$ori]_t4 temp$$_t4 $t4file

	@ mode = 1024 + 3
	$bindir/imgreg_4dfp $mpr"_g15" none        $mri"_g15" $mri"_mskz" $t4file $mode >> $log
	if ($status < 0) exit $status
	@ mode = 1024 + 3
	$bindir/imgreg_4dfp $mpr"_g15" $mpr"_mskt" $mri"_g15" $mri"_mskz" $t4file $mode >> $log
	@ mode = 2048 + 3 + 8 + 64
	$bindir/imgreg_4dfp $mpr"_g15" $mpr"_mskt" $mri"_g15" $mri"_mskz" $t4file $mode >> $log
	@ mode = 8192 + 2048 + 3 + 8 + 64
	$bindir/imgreg_4dfp $mpr"_g15" $mpr"_mskt" $mri"_g15" $mri"_mskz" $t4file $mode >> $log
	$bindir/imgreg_4dfp $mpr"_g15" $mpr"_mskt" $mri"_g15" $mri"_mskz" $t4file $mode >> $log
	if (! $debug) /bin/rm $mpr"_msk"* $mpr"_g"* $mri"_msk"* $mri"_g"* temp$$_t4
endif

$bindir/t4_mul $mri"_to_"$mpr"_t4" $mpr"_to_711-2A_t4"	$mri"_to_711-2A_t4"
$bindir/t4_mul $mri"_to_711-2A_t4" $refdir/to_711-2B_t4	$mri"_to_711-2B_t4"

echo "start time "$date0
echo "end   time "`date`
exit
