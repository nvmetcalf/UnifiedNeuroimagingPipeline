#!/bin/csh -f
#$Header: /data/petsun4/data1/solaris/csh_scripts/RCS/conc_cat,v 1.3 2018/08/17 05:34:12 avi Exp $
#$Log: conc_cat,v $
# Revision 1.3  2018/08/17  05:34:12  avi
# invoke with -f
#
# Revision 1.2  2014/02/23  05:31:09  avi
# rec
#
# Revision 1.1  2014/02/23  05:08:46  avi
# Initial revision
#
set rcsid	= '$Id: conc_cat,v 1.3 2018/08/17 05:34:12 avi Exp $'
echo $rcsid
set program = $0; set program = $program:t

@ verb = 0
set old = ()
@ k = 0
@ i = 1
while ($i <= ${#argv})
	set swi = `echo $argv[$i] | awk '{print substr($1,1,2)}'`
	set arg = `echo $argv[$i] | awk '{print substr($0,3)}'`
	switch ($swi)
		case -v:
			@ verb++;			breaksw;
		default:
		switch ($k)
			case 0:
				set conc = $argv[$i];		@ k++; breaksw;
			default:
				set old = ($old $argv[$i]);	@ k++; breaksw;
		endsw
	endsw
	@ i++
end
if ($k < 2) goto USAGE
echo combining $old

#######################################
# check existence and count total files
#######################################
@ nfile = 0
@ nconc = 0
foreach x ($old)
	if ($x:e == "conc") set x = $x:r
	set x = $x.conc
	if (! -e $x) then
		echo $program": "$x not found
		exit -1
	endif
	@ nconc++
	set old[$nconc] = $x
	@ nfile += `cat $x | gawk '/number_of_files:/{print $NF}'`
end

#################
# create new conc
#################
if ($conc:e == "conc") set conc = $conc:r
set conc = $conc.conc
echo "number_of_files: "$nfile		>! $conc
if ($status) exit $status
foreach x ($old)
	cat $x | gawk '/file:/{print}'	>> $conc
end
cat $conc

#####
# rec
#####
echo rec $conc `date` `whoami` >!	$conc.rec
echo $program $argv >>			$conc.rec
echo $rcsid >>				$conc.rec
@ k = 1
while ($k <= $nconc)
	if (-r $old[$k].rec) then
		cat $old[$k].rec >>	$conc.rec
	else
		echo $old[$k].rec not found
	endif
	@ k++
end
echo endrec `date` `whoami` >>		$conc.rec

exit

USAGE:
echo "usage:	"$program" <new conc> <old conc1> <old conc2> ..."
echo " e.g.:	"$program" conc_cat TC27621ab_faln_dbnd_xr3d_atl.conc TC27621a_faln_dbnd_xr3d_atl.conc TC27621b_faln_dbnd_xr3d_atl.conc"
echo "	option"
exit 1
