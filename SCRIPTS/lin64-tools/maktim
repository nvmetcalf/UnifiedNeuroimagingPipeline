#$Header: /data/petsun4/data1/solaris/csh_scripts/RCS/maktim,v 1.3 2018/08/17 05:50:14 avi Exp $
#$Log#
#!/bin/csh -f

set program = `basename $0`

if (${#argv} < 1) then
        echo "Usage:	$program <ecat> [options]"
	echo "options"
        echo "	skip	skip missing frames"
        exit 1
endif

if (! -e $1) then
	echo "${program}: $1 not found"
	exit -1
endif
@ skip = 0
if ($#argv > 1) then
	foreach option ($argv[2-])
		switch ($option)
		case skip:
			@ skip++;	breaksw;
		endsw
	end
endif

set nf   = `ecat_header $1 | gawk '/Number of Frames/ {print $NF}'`

set root = $1:r
set scan = $root:t

set tim  = $scan.tim
if (-e $tim) goto CAT

if (! $skip) then
	touch $tim
	@ f = 1
	while ($f <= $nf)
		printf '%4d' $f >> $tim
		ecat_header $1 $f | gawk '/Frame Duration/{d=$(NF-1)/1000} /Frame Start Time/{s=$(NF-1)/1000} END {printf ("%10.2f%10.2f\n", s, d)}' >> $tim
		@ f ++
	end
else
	ecat_header $1 0 | gawk '/Frame Duration/{d=$(NF-1)/1000} /Frame Start Time/{s=$(NF-1)/1000} /Frame Number/{printf ("%4d%10.2f%10.2f\n", $NF, s, d)}' >! $tim
endif

CAT:
echo $tim
cat  $tim
exit 0
