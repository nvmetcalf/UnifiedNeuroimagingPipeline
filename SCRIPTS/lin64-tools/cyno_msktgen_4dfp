#!/bin/csh -f
#$Header: /data/petsun4/data1/solaris/csh_scripts/RCS/cyno_msktgen_4dfp,v 1.3 2018/08/17 05:43:10 avi Exp $
#$Log: cyno_msktgen_4dfp,v $
# Revision 1.3  2018/08/17  05:43:10  avi
# invoke with -f
#
# Revision 1.2  2013/09/07  23:45:22  avi
# option -T
#
# Revision 1.1  2013/09/03  22:49:38  avi
# Initial revision
#
set idstr	= '$Id: cyno_msktgen_4dfp,v 1.3 2018/08/17 05:43:10 avi Exp $'
set program	= $0; set program = $program:t;
set wrkdir	= $cwd
set thresh	= 200
set target      = /data/petsun59/data2/macaqueF6/macaqueF6
set refmsk      = /data/petsun59/data2/macaqueF6/macaqueF6_brain_g10
if (! -e $refmsk.4dfp.img || ! -e $refmsk.4dfp.ifh) then
	echo $atlas mask not found
	exit -1
endif

echo $idstr
@ n = ${#argv}
@ m = 1
@ k = 0
while ($m <= $n)
	switch ($argv[$m])
	case -T*:
		set target = `echo $argv[$m] | awk '{print substr($1,3)}'`
		if ($target:e == "img")  set target = $target:r
		if ($target:e == "4dfp") set target = $target:r
		breaksw;
	default:
		switch ($k)
		case 0:
			set img = $argv[$m]; @ k++; breaksw;
		case 1:
			echo $argv[$m] | awk '{q=$1/$1;}' >& /dev/null
			if ($status) then
				echo invalid threshold numerical format
			    	exit -1
			else
				set thresh =  $argv[$m];
			endif
			breaksw;
		default:
			breaksw;
		endsw
	endsw
	@ m++
end
if ($k < 1) then
	echo "Usage:	"$program" <(4dfp) image> [threshold]"
	echo " e.g.,	"$program" davy_100401_mpr1_crop_gfc"
	echo "	options"
	echo "	-T<str>	specify non-default atlas-representative target (default = /data/petsun59/data2/macaqueF6/macaqueF6)"
	echo "N.B.:	atlas transform t4file must exist in the wokring directory"
	echo "N.B.:	default threshold = 200"
	echo "N.B.:	specify a higher threshold, e.g., 400, for a tighter mask and vice versa"
	echo "N.B.:	threshold = 500 approximates the brain edge"
	exit 1
endif
echo target	= $target
echo threshold	= $thresh

if ($img:e == "img")	set img = $img:r
if ($img:e == "4dfp")	set img = $img:r
if (! -e $img.4dfp.img || ! -e $img.4dfp.ifh) then
	echo $img.4dfp.img or $img.4dfp.ifh not found
	exit -1
endif
set t4file = $img"_to_"${target:t}_t4
if (! -e $t4file) then
	echo $t4file not found
	exit -1
endif

echo computing tailored mask ${img}_mskt
t4_inv -u $t4file atlas_to_${img}_t4
if ($status) exit $status
t4img_4dfp atlas_to_${img}_t4 $refmsk temp$$ -O$img
if ($status) exit $status
/bin/rm atlas_to_${img}_t4
zero_lt_4dfp $thresh temp$$ ${img}_mskt
if ($status) exit $status
/bin/rm temp$$.4dfp*

exit
