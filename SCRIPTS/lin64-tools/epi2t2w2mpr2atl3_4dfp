#!/bin/csh -f
# $Header: /data/petsun4/data1/solaris/csh_scripts/RCS/epi2t2w2mpr2atl3_4dfp,v 1.3 2018/08/17 05:46:09 avi Exp $
# $Log: epi2t2w2mpr2atl3_4dfp,v $
# Revision 1.3  2018/08/17  05:46:09  avi
# invoke with -f
#
# Revision 1.2  2010/09/01  22:50:15  avi
# *** empty log message ***
#
# Revision 1.1  2010/09/01  22:46:14  avi
# Initial revision
#
# Revision 1.0  2010/08/31  19:12:24   mostly avi + some shahm
# transformation without the mask - important for transforming contrasted Stealth MPR to atlas-space
#

set idstr = '$Id: epi2t2w2mpr2atl3_4dfp,v 1.3 2018/08/17 05:46:09 avi Exp $'
set program = $0; set program = $program:t;

echo $idstr
if (${#argv} < 3) then
	echo "Usage:	"$program "<(4dfp) epi_anat> <(4dfp) t2w_anat> <(4dfp) mpr_anat> [options]"
	echo "e.g.,	"$program "stem9_anat_ave stem9_643-2 stem9_654-3 711-2B"
	echo "e.g.,	"$program "stem9_anat_ave stem9_654-3 useold -T/data/cninds01/atlas/NP765"
	echo "	options"
	echo "	useold				suppress recomputation of exiting t4files"
	echo "	setecho				you are an idiot if you can't figure this out"
	echo "	711-2?				specify atlas representative target"
	echo "	-T<Target including path>	specify atlas representative target"
	exit 1
endif

set date0	= `date`
set usr		= `whoami`
set wrkdir 	= $cwd
set oristr	= (T C S)

set target		= 711-2B
set targetswitch	= -T$REFDIR/$target
@ useold	= 0
@ debug		= 0
foreach x (${argv[4-]})
	if ($x == "debug")	@ debug++
	if ($x == "useold")	@ useold++		# use existing t4files if present
	switch ($x)
		case 711-2?:
			set target       = $x;
			set targetswitch = -T$REFDIR/$target
			breaksw;
		setecho:
			set echo; 	breaksw;
		case -T*:
			set targetpath   = `echo $x | awk '{print substr($1,3)}'`;
			set target       = $targetpath:t;
			if ($target:e == "img")  set target = $target:r
			if ($target:e == "4dfp") set target = $target:r
			set targetswitch = "-T"$targetpath
			breaksw;
	endsw
end

set xxx = ($1 $2 $3)
@ k = 1
while ($k <= 3)
	if ($xxx[$k]:e == "img")	set xxx[$k] = $xxx[$k]:r
	if ($xxx[$k]:e == "4dfp")	set xxx[$k] = $xxx[$k]:r
	if (! -e $xxx[$k].4dfp.img || ! -e $xxx[$k].4dfp.ifh) then
		echo $xxx[$k] not found
		exit -1
	endif
	@ k++
end
set epi		= $xxx[1]
set t2w		= $xxx[2]
set mpr		= $xxx[3]

echo atlas representative target = $target	atlas space = 711-2B

set log = $mpr"_"$program.log
date >!				$log
echo $0 $argv[1-] >>		$log
echo $idstr >>			$log
echo epi = $epi >>		$log
echo t2w = $t2w >>		$log
echo mpr = $mpr >>		$log
echo "target="$targetpath >>	$log
echo "atlasspsace = 711-2B" >>	$log
tail -50 $log

##################
# MP-RAGE->$target
##################
set t4file = $mpr"_to_"$target"_t4"
if (! $useold || ! -e $t4file) then
	echo computing $t4file
	echo	mpr2atl1_4dfp $mpr $targetswitch
		mpr2atl1_4dfp $mpr $targetswitch
	if ($status) exit $status
endif

set t4file = $t2w"_to_"$mpr"_t4"
if (! $useold || ! -e $t4file) then
	msktgen_4dfp $mpr $targetswitch
	if ($status < 0) exit $status

	@ ori = `awk '/orientation/{print $NF - 1}' $mpr.4dfp.ifh`
	t4_inv $RELEASE/$oristr[$ori]_t4 temp$$_t4
	@ ori = `awk '/orientation/{print $NF - 1}' $t2w.4dfp.ifh`
	t4_mul $RELEASE/$oristr[$ori]_t4 temp$$_t4 $t4file

	@ mode = 4096 + 3		# brute-force displacement grid search
	imgreg_4dfp $mpr none        $t2w none $t4file $mode >> $log
	@ mode = 1024 + 3
	imgreg_4dfp $mpr none        $t2w none $t4file $mode >> $log
	@ mode = 2048 + 3
	imgreg_4dfp $mpr $mpr"_mskt" $t2w none $t4file $mode >> $log
	@ mode = 2048 + 3		
	imgreg_4dfp $mpr $mpr"_mskt" $t2w none $t4file $mode >> $log
	@ mode = 8192 + 2048 + 3
	imgreg_4dfp $mpr $mpr"_mskt" $t2w none $t4file $mode >> $log
	if ($status < 0) exit $status
	if (! $debug) /bin/rm $mpr"_mskt"*
endif

set t4file = $epi"_to_"$t2w"_t4"
if (! $useold || ! -e $t4file) then
	@ ori = `awk '/orientation/{print $NF - 1}' $t2w.4dfp.ifh`
	t4_inv $RELEASE/$oristr[$ori]_t4 temp$$_t4
	@ ori = `awk '/orientation/{print $NF - 1}' $epi.4dfp.ifh`
	t4_mul $RELEASE/$oristr[$ori]_t4 temp$$_t4 $t4file

	@ mode = 4096 + 3		# brute-force displacement grid search
	imgreg_4dfp $t2w none $epi none $t4file $mode >> $log
	@ mode = 1024 + 3
	imgreg_4dfp $t2w none $epi none $t4file $mode >> $log
	@ mode = 1024 + 3
	imgreg_4dfp $t2w none $epi none $t4file $mode >> $log
	@ mode = 3072 + 3
	imgreg_4dfp $t2w none $epi none $t4file $mode >> $log
	@ mode = 8192 + 2048 + 3
	imgreg_4dfp $t2w none $epi none $t4file $mode >> $log
	if ($status < 0) exit $status
	if (! $debug) /bin/rm none* temp$$*
endif

t4_mul $t2w"_to_"$mpr"_t4" $mpr"_to_"$target"_t4"	$t2w"_to_"$target"_t4"
t4_mul $epi"_to_"$t2w"_t4" $t2w"_to_"$target"_t4"	$epi"_to_"$target"_t4"
t4_mul $epi"_to_"$t2w"_t4" $t2w"_to_"$mpr"_t4"		$epi"_to_"$mpr"_t4"

echo "start time "$date0
echo "end   time "`date`
exit
