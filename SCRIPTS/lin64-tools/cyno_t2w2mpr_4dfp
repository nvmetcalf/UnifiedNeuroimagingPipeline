#!/bin/csh -f
# $Header: /data/petsun4/data1/solaris/csh_scripts/RCS/cyno_t2w2mpr_4dfp,v 1.3 2018/08/17 05:43:18 avi Exp $
# $Log: cyno_t2w2mpr_4dfp,v $
# Revision 1.3  2018/08/17  05:43:18  avi
# invoke with -f
#
# Revision 1.2  2013/09/08  01:14:38  avi
# option -T
# correct several bugs
#
# Revision 1.1  2013/09/08  00:39:01  avi
# Initial revision
#
set idstr = '$Id: cyno_t2w2mpr_4dfp,v 1.3 2018/08/17 05:43:18 avi Exp $'
echo $idstr
set program = $0; set program = $program:t

set target      = /data/petsun59/data2/macaqueF6/macaqueF6
@ debug = 0
@ m = 1
@ k = 0
while ($m <= ${#argv})
	switch ($argv[$m])
	case setecho:
		set echo;		breaksw;
	case debug:
		@ debug++;		breaksw;
	case -T*:
		set target = `echo $argv[$m] | awk '{print substr($1,3)}'`
		if ($target:e == "img")  set target = $target:r
		if ($target:e == "4dfp") set target = $target:r
		breaksw;
	default:
		switch ($k)
		case 0:
			set mpr = $argv[$m];	@ k++; breaksw;
		case 1:
			set t2w = $argv[$m];	@ k++; breaksw;
		default:
			breaksw;
		endsw
	endsw
	@ m++
end
if ($k < 2) goto USAGE
echo "atlas representative target="$target

if ($t2w:e == "img") set t2w = $t2w:r; if ($t2w:e == "4dfp") set t2w = $t2w:r;
if (! -e $t2w.4dfp.ifh || ! -e $t2w.4dfp.img) then
	echo $program": "$t2w not found
	exit -1
endif
if ($mpr:e == "img") set mpr = $mpr:r; if ($mpr:e == "4dfp") set mpr = $mpr:r;
if (! -e $mpr.4dfp.ifh || ! -e $mpr.4dfp.img) then
	echo $program": "$mpr not found
	exit -1
endif
if (! -e $target.4dfp.ifh || ! -e $target.4dfp.img) then
	echo $program": "$target not found
	exit -1
endif
set refmsk      = /data/petsun59/data2/macaqueF6/macaqueF6_brain_g10z
if (! -e $refmsk.4dfp.ifh || ! -e $refmsk.4dfp.img) then
	echo $program": "$refmsk not found
	exit -1
endif

set t4file	= ${t2w:t}_to_${mpr:t}_t4
set log		= ${t2w:t}_to_${mpr:t}.log
echo $program $argv[1-]	>! $log
echo $idstr		>> $log
date			>> $log
echo "mpr="$mpr		>> $log
echo "t2w="$t2w		>> $log
echo "target="$target	>> $log

cyno_msktgen_4dfp $mpr 100
if ($status) exit $status
set oristr =	(T C S)
@ ori = `awk '/orientation/{print $NF - 1}' $mpr.4dfp.ifh`
t4_inv $RELEASE/$oristr[$ori]_t4 temp$$_t4
@ ori = `awk '/orientation/{print $NF - 1}' $t2w.4dfp.ifh`
t4_mul $RELEASE/$oristr[$ori]_t4 temp$$_t4 $t4file
/bin/rm temp$$_t4

set modes	= (0 0 0 0)
@ modes[1]	= 4096 + 3
@ modes[2]	= 2048 + 3
@ modes[3]	=  512 + 3 
@ modes[4]	=  512 + 3 + 8192
set usemsk	= (none none ${mpr}_mskt ${mpr}_mskt)
@ k = 1
while ($k <= ${#modes})
	echo	imgreg_4dfp  $mpr $usemsk[$k] $t2w none $t4file $modes[$k] >> $log
		imgreg_4dfp  $mpr $usemsk[$k] $t2w none $t4file $modes[$k] >> $log
	if ($status) exit $status
	@ k++
end
t4_mul $t4file ${mpr}_to_${target:t}_t4 ${t2w}_to_${target:t}_t4
if ($status) exit $status

t4img_4dfp ${t2w:t}_to_${target:t}_t4 $t2w	${t2w}_on_${target:t} -O$target
ifh2hdr	 -r1000					${t2w}_on_${target:t}

if (! $debug) then
	/bin/rm $mpr"_mskt".4dfp*
endif
exit 0

USAGE:
echo "Usage:	"$program" <(4dfp) MP-RAGE> <(4dfp) t2w> [options]"
echo "e.g.:	"$program" davy_100401_mpr1_crop_gfc davy_100401_t2w"
echo "	options"
echo "	setecho	set echo"
echo "	debug	debug mode"
echo "	-T<str>	specify non-default atlas-representative target (default = /data/petsun59/data2/macaqueF6/macaqueF6)"
echo "N.B.:	"$program" assumes that <4dfp mprage> is in the current working directory"
echo "	and that its atlas transform, e.g., davy_100401_mpr1_crop_gfc_to_macaqueF6_t4"
echo "	exists and is in the current working directory"
exit 1
